context ProductContext is {
  include "productSources"

  type ProductDetails is any of {TicketDetails}
  type ProductStatus is any of {Draft, Active, InActive, Deleted} briefly "Note: Delete is not the same as in Org, Tenant, etc because there is no PII. Here it is more like Terminate"
  type SKU is Id(ProductContext.Product)
  type TicketDetails is one of {ReservedTicket, RestrictedTicket, OpenTicket}

  type ReservedTicket is {
    section: String,
    row: String,
    seat: String,
    event: EventId
  }
  type RestrictedTicket is {
    section: String,
    event: EventId
  }
  type OpenTicket is {
    event: EventId
  }
  type ProductInfo is {
    name: String briefly "Required for non-Draft state",
    description: String briefly "Required for non-Draft state",
    productDetails: ProductDetails briefly "Required for non-Draft state",
    image: String*,
    price: Number briefly "Required for non-Draft state",
    cost: Number briefly "Required for non-Draft state",
    store: StoreContext.StoreId briefly "Required for non-Draft state"
  }
  type ProductInfoUpdate is {
    name: String?,
    description: String?,
    productDetails: ProductDetails?,
    image: String*,
    price: Number?,
    cost: Number?,
    store: StoreContext.StoreId?
  }
  type ProductMetaInfo is {
    createdBy: MemberId,
    createdOn: Date,
    lastModifiedBy: MemberId,
    lastModifiedOn: Date
  }
  command CreateProduct is {sku: SKU, info: ProductContext.ProductInfo, onBehalfOf: MemberId}
  event ProductCreated is {sku: SKU, info: ProductContext.ProductInfo, meta: ProductMetaInfo}
  command UpdateProductInfo is {sku: SKU, info: ProductContext.ProductInfoUpdate, onBehalfOf: MemberId}
  event ProductInfoUpdated is {sku: SKU, info: ProductContext.ProductInfo, meta: ProductMetaInfo}
  command DeleteProduct is {sku: SKU, onBehalfOf: MemberId}
  event ProductDeleted is {sku: SKU}
  command ActivateProduct is {sku: SKU, onBehalfOf: MemberId}
  event ProductActivated is {sku: SKU}
  command InactivateProduct is {sku: SKU, onBehalfOf: MemberId}
  event ProductInactivated is {sku: SKU}
  query GetProductInfo is {sku: SKU}
  result ProductInfoResult is {info: ProductContext.ProductInfo, meta: ProductContext.ProductMetaInfo}
  query GetTicketsForEvent is {event: EventId} //TODO: Move this query/result to a projector?
  result TicketsForEvent is {event: EventId, tickets: SKU*}

  entity Product is {
    option event-sourced
    handler ProductHandler is {
      on command ProductContext.CreateProduct {
        morph entity ProductContext.Product to state DraftProductState with record DraftProduct
      }
      on other {
        error "Only the CreateProduct command is allowed in the default state."
      }
    }

    record DraftProduct is {
      sku: SKU,
      info: ProductContext.ProductInfo,
      meta: ProductMetaInfo
    }
    state DraftProductState of Product.DraftProduct is {
      handler CreatedProductHandler is {
        on command ProductContext.CreateProduct {
          error "Product already created."
        }
        on command UpdateProductInfo{
          "Copy fields that are provided from UpdateProductInfo.info into DefinedProductState.info fields"
          "adjust meta fields to reflect the change set"
          "yield ProductInfoUpdated event with new info"
        }
        on command DeleteProduct{
          set field DraftProductState.meta to "ProductContext.ProductMetaInfo"
          send event ProductDeleted
            to outlet ProductEventsSource.Events
          morph entity ProductContext.Product to state DeletedProductState with record DeletedProduct
        }
        on command ActivateProduct{
          if "not all required fields are filled in" then {
            "return a list of non-filled in fields"
          } else {
            // "all required fields are present"
            set field DraftProductState.meta to "ProductContext.ProductMetaInfo"
            send event ProductActivated to outlet ProductEventsSource
            morph entity Product to state DefinedProductState with record DefinedProduct
          }
        }
        on command InactivateProduct{
          error "cannot inactivate a draft product"
        }
        on query GetProductInfo{
          "yield ProductInfoResult message"
        }
      }
    }

    record DefinedProduct is {
      sku: SKU,
      info: ProductContext.ProductInfo,
      meta: ProductContext.ProductMetaInfo
    }
    state DefinedProductState of Product.DefinedProduct is {
      handler ActiveProductHandler is {
        on command ProductContext.CreateProduct {
          error "Product already created."
        }
        on command ProductContext.UpdateProductInfo{
          "Copy fields that are provided from UpdateProductInfo.info into DefinedProductState.info fields"
          "adjust meta fields to reflect the change set"
          "yield ProductInfoUpdated event with new info"
        }
        on command ProductContext.DeleteProduct{
          set field DefinedProductState.meta to "ProductContext.ProductMetaInfo"
          send event ProductDeleted to outlet ProductEventsSource.Events
          morph entity ProductContext.Product to state DeletedProductState with record DeletedProduct
        }
        on command ProductContext.ActivateProduct{
          error "Product is already active"
        }
        on command ProductContext.InactivateProduct {
          set field DefinedProductState.meta to "ProductContext.ProductMetaInfo"
          send event ProductInactivated to outlet ProductEventsSource.Events
          become entity ProductContext.Product to handler Product.DefinedProductState.InactiveProductHandler
        }
        on query GetProductInfo{
          "yield ProductInfoResult message"
        }
      }
      handler InactiveProductHandler is {
        on command ProductContext.CreateProduct {
          error "Product already created."
        }
        on command ProductContext.UpdateProductInfo{
          "Copy fields that are provided from UpdateProductInfo.info into DefinedProductState.info fields"
          "adjust meta fields to reflect the change set"
          "yield ProductInfoUpdated event with new info"
        }
        on command ProductContext.DeleteProduct{
          set field DefinedProductState.meta to "ProductContext.ProductMetaInfo"
          send event ProductDeleted to outlet ProductEventsSource.Events
          morph entity ProductContext.Product to state ProductContext.Product.DeletedProductState with record DeletedProduct
        }
        on command ProductContext.ActivateProduct{
          set field DefinedProductState.meta to "ProductContext.ProductMetaInfo"
          send event ProductActivated to outlet ProductEventsSource.Events
          become entity ProductContext.Product to handler Product.DefinedProductState.ActiveProductHandler
        }
        on command ProductContext.InactivateProduct{
          error "Product is already Inactive"
        }
        on query GetProductInfo{
          "yield ProductInfoResult message"
        }
      }
    }

    record DeletedProduct is {
      sku: SKU,
      name: String
    }
    state DeletedProductState of Product.DeletedProduct is {
      handler DeletedProductHandler is {
        on command ProductContext.CreateProduct {
          error "Product already created."
        }
        on command ProductContext.UpdateProductInfo{
          error "Cannot update a Deleted product"
        }
        on command ProductContext.DeleteProduct{
          error "Product already deleted"
        }
        on command ProductContext.ActivateProduct{
          error "Cannot activate a deleted product. Consider creating a new product instead."
        }
        on command ProductContext.InactivateProduct{
          error "Cannot inactivate a Deleted product."
        }
        on query GetProductInfo{
          "yield ProductInfoUpdate with only sku and name populated"
        }
      }
    }
  }
}

  //ProductDetails is select ReservedTicket | RestrictedTicket | OpenTicket | FoodItem | Beverage
  //Units is any of {Grams, Kilograms, Pounds, Ounces, Milliliters, Liters}
  //Condiments is any of {Catsup, Mustard, Mayonnaise, Fry Sauce, Relish, Onion, Pickle, Jalapeno}
  /*type FoodItem is {
    PrimaryUnitOfMeasure: Units,
    PrimarySize: Number,
    SecondaryUnitOfMeasure: Units,
    SecondarySize: Number,
    Ingredients: String,
    ServingSize: Number,
    ServingsPerPackage: Number,
    NutritionInfo: String,
    AllergyInfo: String,
    Condiments: Condiments*
    PreparationNotes: String
  }
  type Beverage is {
    sugarFree: Boolean,
    alcoholic: Boolean,
    unitOfMeasure: Units,
    size: Number,
    addIce: Boolean
  }*/
