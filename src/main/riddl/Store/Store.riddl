context StoreContext is {
  include "storeSources"

  type StoreStates is any of {Draft, Ready, Open, Closed, Deleted}
  type StoreId is Id(StoreContext.Store)

  type Info is {
    name: String briefly "Required",
    description: String briefly "Required",
    products: SKU+ briefly "Required",
    event: EventId?,
    venue: VenueId briefly "Required",
    location: Venues.LocationId briefly "Required",
    sponsoringOrg: OrganizationId briefly "Required"
  }

  type MetaInfo is {
    createdBy: MemberId,
    createdOn: Date,
    lastModifiedBy: MemberId,
    lastModifiedOn: Date,
    state: StoreStates
  }

  type EditableInfo is {
    name: String?,
    description: String?,
    products: SKU*,
    event: EventId?,
    venue: VenueId?,
    location: Venues.LocationId?,
    sponsoringOrg: OrganizationId?
  }

  command CreateStore is {storeId: StoreContext.StoreId, info: StoreContext.EditableInfo, creatingMember: MemberId}
  event StoreCreated is {storeId: StoreContext.StoreId, info: StoreContext.EditableInfo, meta: StoreContext.MetaInfo}
  command EditStoreInfo is {storeId: StoreContext.StoreId, info: StoreContext.EditableInfo, updatingMember: MemberId}
  event StoreInfoEdited {storeId: StoreContext.StoreId, info: StoreContext.Info, meta: StoreContext.MetaInfo}
  command MakeStoreReady is {storeId: StoreContext.StoreId, updatingMember: MemberId}
  event StoreIsReady is {storeId: StoreContext.StoreId, meta: StoreContext.MetaInfo}
  command DeleteStore is {storeId: StoreContext.StoreId, deletingMember: MemberId}
  event StoreDeleted is {storeId: StoreContext.StoreId, meta: StoreContext.MetaInfo}
  command OpenStore is {storeId: StoreContext.StoreId, openingMember: MemberId}
  event StoreOpened is {storeId: StoreContext.StoreId, info: StoreContext.Info, meta: StoreContext.MetaInfo}
  command CloseStore is {storeId: StoreContext.StoreId, closingMember: MemberId}
  event StoreClosed is {storeId: StoreContext.StoreId, info: StoreContext.Info, meta: StoreContext.MetaInfo}
  command TerminateStore is {storeId: StoreContext.StoreId, terminatingMember: MemberId}
  event StoreTerminated is {storeId: StoreContext.StoreId, lastMeta: StoreContext.MetaInfo}
  command AddProductsToStore is {storeId: StoreContext.StoreId, products: SKU+, addingMember: MemberId}
  event ProductsAddedToStore is {storeId: StoreContext.StoreId, info: StoreContext.Info, meta: StoreContext.MetaInfo}
  command RemoveProductsFromStore is {storeId: StoreContext.StoreId, product: SKU+, removingMember: MemberId}
  event ProductsRemovedFromStore is {storeId: StoreContext.StoreId, info: StoreContext.Info, meta: StoreContext.MetaInfo}
  //command IncrementProductInventory is {???} //TODO: Should we pull in the Nike Inventory demo that Sean built?
  //command DecrementProductInventory is {???}
  query GetStoreInfo is {???}
  result StoreInfoResult is {???}
  query GetProductsInStore is {store: StoreContext.StoreId}
  result ProductsInStore is {products: SKU*}

  entity Store is {
    option event-sourced
    option available

    handler DefaultStoreHandler is {
      on command CreateStore {
        send event StoreCreated to outlet StoreEventsSource.Events
        morph entity StoreContext.Store to state Store.DraftStore with record Store.DraftStoreData
      }
      on other {
        error "Only the CreateStore command is allowed in the default state"
      }
    }

    record DraftStoreData is {
      id: StoreContext.StoreId,
      info: StoreContext.EditableInfo,
      meta: StoreContext.MetaInfo
    }
    state DraftStore of Store.DraftStoreData is {
      handler DraftStoreHandler is {
        on command CreateStore {
          error "Store already created"
        }
        on command EditStoreInfo {
          "copy fields from info to Draft info fields"
          "adjust meta fields to reflect the change"
          "yield the StoreUpdated event"
        }
        on command DeleteStore {
          "remove all attributes not defined in the Deleted state"
          "adjust meta fields to reflect change"
          "yield the StoreDeleted event"
          morph entity StoreContext.Store to state DeletedStore with record DeletedStoreData
        }
        on command MakeStoreReady {
          "yield the StoreIsReady event"
          morph entity StoreContext.Store to state CreatedStore with record CreatedStoreData
        }
        on command OpenStore {
          error "store must be moved to the Ready state. All required attributes must be provided."
        }
        on command CloseStore {
          error "A store in Draft state may not be closed" // FIXME: Really? Why not?
        }
        on command AddProductsToStore {
          "add provided products to store definition"
          "and adjust meta fields to reflect the change"
          "yield the ProductsAddedToStore event"
          // TODO: Add cases where there is invalid product
        }
        on command RemoveProductsFromStore {
          "remove provided products from store"
          "adjust meta fields to reflect the change"
          "yield the ProductsRemovedFromStore event"
          // TODO: Add case where product to be removed doesn't exist in store
        }
      }
    }

    record CreatedStoreData is {
      id: StoreContext.StoreId,
      info: StoreContext.Info,
      meta: StoreContext.MetaInfo
    }
    state CreatedStore of CreatedStoreData is {
      handler ReadyStoreHandler is {
        on command CreateStore {
          error "Store already created"
        }
        on command EditStoreInfo {
          "copy fields from info to Draft info fields"
          "adjust meta fields to reflect the change"
          "yield the StoreUpdated event"
        }
        on command DeleteStore {
          error "Store must be closed before deleting it"
        }
        on command MakeStoreReady {
          error "Store must be in Draft state before being made Ready"
        }
        on command OpenStore {
          set field Store.CreatedStore.meta to "StoreContext.MetaInfo"
          send event StoreOpened to outlet StoreEventsSource.Events
          become entity StoreContext.Store to handler StoreContext.Store.CreatedStore.OpenStoreHandler
        }
        on command CloseStore {
          "ensure all orders associated with store are Delivered or Cancelled before closing store"
          set field CreatedStore.meta to "StoreContext.MetaInfo"
          send event StoreClosed to outlet StoreEventsSource.Events
          become entity StoreContext.Store to handler StoreContext.Store.CreatedStore.ClosedStoreHandler
        }
        on command AddProductsToStore {
          "add provided products to store definition"
          "and adjust meta fields to reflect the change"
          "yield the ProductsAddedToStore event"
          // TODO: Add cases where there is invalid product
        }
        on command RemoveProductsFromStore {
          "remove provided products from store"
          "adjust meta fields to reflect the change"
          "yield the ProductsRemovedFromStore event"
          // TODO: Add case where product to be removed doesn't exist in store
        }
        on command TerminateStore {
          set field CreatedStore.meta to "StoreContext.MetaInfo"
          morph entity StoreContext.Store to state TerminatedStore with record TerminatedStoreData
            // (lastMeta = @CreatedStoreState.meta)
        }
      }

      handler OpenStoreHandler is {
          on command CreateStore {
            error "Store already created"
          }
          on command EditStoreInfo {
            "copy fields from info to Draft info fields"
            "adjust meta fields to reflect the change"
            "yield the StoreUpdated event"
          }
          on command MakeStoreReady {
            error "Store must be in Draft state before being made Ready"
          }
          on command DeleteStore {
            error "Store must be closed before deleting it"
          }
          on command OpenStore {
            error "Store is already opened"
          }
          on command CloseStore {
            "ensure all orders associated with store are Delivered or Cancelled before closing store"
            set field CreatedStore.meta to "StoreContext.MetaInfo"
            send event StoreClosed to outlet StoreEventsSource.Events
            become entity StoreContext.Store to handler StoreContext.Store.CreatedStore.ClosedStoreHandler
          }
          on command AddProductsToStore {
            "add provided products to store definition"
            "and adjust meta fields to reflect the change"
            "yield the ProductsAddedToStore event"
            // TODO: Add cases where there is invalid product
          }
          on command RemoveProductsFromStore {
            "remove provided products from store"
            "adjust meta fields to reflect the change"
            "yield the ProductsRemovedFromStore event"
            // TODO: Add case where product to be removed doesn't exist in store
          }
          on command TerminateStore {
            set field CreatedStore.meta to "StoreContext.MetaInfo"
            morph entity StoreContext.Store to state TerminatedStore with record TerminatedStoreData
              // (lastMeta = @CreatedStoreState.meta)
          }
        }

        handler ClosedStoreHandler is {
          on command CreateStore {
            error "Store already created"
          }
          on command EditStoreInfo {
            "copy fields from info to Draft info fields"
            "adjust meta fields to reflect the change"
            "yield the StoreUpdated event"
          }
          on command MakeStoreReady {
            error "Store must be in Draft state before being made Ready"
          }
          on command DeleteStore {
            set field CreatedStore.meta to "StoreContext.MetaInfo"
            send event StoreDeleted to outlet StoreEventsSource.Events
            morph entity StoreContext.Store to state DeletedStore with record DeletedStoreData
          }
          on command OpenStore {
            set field CreatedStore.meta to "StoreContext.MetaInfo"
            send event StoreOpened to outlet StoreEventsSource.Events
            become entity StoreContext.Store to handler Store.CreatedStore.OpenStoreHandler
          }
          on command CloseStore {
            error "Store is already closed."
          }
          on command AddProductsToStore {
            "add provided products to store definition"
            "and adjust meta fields to reflect the change"
            "yield the ProductsAddedToStore event"
            // TODO: Add cases where there is invalid product
          }
          on command RemoveProductsFromStore {
            "remove provided products from store"
            "adjust meta fields to reflect the change"
            "yield the ProductsRemovedFromStore event"
            // TODO: Add case where product to be removed doesn't exist in store
          }
          on command TerminateStore {
            set field CreatedStore.meta to "StoreContext.MetaInfo"
            morph entity StoreContext.Store to state TerminatedStore with record TerminatedStoreData
              // (lastMeta = @CreatedStoreState.meta)
        }
      }
    }

    record DeletedStoreData is {
      storeName: String,
      event: EventId?,
      venue: VenueId,
      location: Venues.LocationId,
      sponsoringOrg: OrganizationId,
      meta: StoreContext.MetaInfo
    }
    state DeletedStore of DeletedStoreData is {
      handler DeletedStoreHandler is {
        on command TerminateStore {
          set field DeletedStore.meta to "StoreContext.MetaInfo"
          morph entity StoreContext.Store to state TerminatedStore with record TerminatedStoreData
            // (lastMeta = @DeletedStoreState.meta)
        }
        on other {
         error "No actions are permitted on a Deleted store"
       }
      }
    }

    record TerminatedStoreData is {
      lastMeta: StoreContext.MetaInfo
    }
    state TerminatedStore of TerminatedStoreData is {
      handler TerminatedStoreHandler is {
        on other {
          error "No actions are permitted on a Terminated store"
        }
      }
    }
  }
}
