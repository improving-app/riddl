application UserInterface is {
  command EstablishOrganization is {
    info: MemberContext.EditableInfo briefly "Details about an organization required for it to exist. See OrganizationContext.Organization.Info for details.",
    tenant: TenantId,
    parent: Parent?,
    members: MemberList,
    owners: OwnerList,
    contacts: ContactList,
    onBehalfOf: MemberContext.MemberId briefly "The Id of the Member who is issuing the command."
  } briefly "If parentOrg is not defined you are creating a BaseOrganization. If it is, you are creating a SubOrganization."

  result OrganizationEstablished is {
    organizationId: OrganizationId briefly "Unique identifier returned by EstablishOrganization command",
    info: OrganizationContext.Info,
    parent: Parent?,
    members: MemberList,
    owners: OwnerList,
    contacts: ContactList,
    meta: OrganizationContext.MetaInfo
  }

  group Organization is {
    input EstablishOrganization acquires command UserInterface.EstablishOrganization

    output DisplayOrganization presents result OrganizationEstablished
  }

  handler OrgHandler is {
    on command EstablishOrganization from user OrgOwner {
      tell command UserInterface.CreateOrganization to context ImprovingApp.OrganizationContext
    }
    on event OrganizationEstablished from context ImprovingApp.OrganizationContext {
      tell result ImprovingApp.OrganizationContext.OrganizationEstablished to
        projector ImprovingApp.OrganizationContext.OrganizationViews
    }
  }

  command RegisterMember is {
    info: MemberContext.EditableInfo briefly "Details about a member required for it to exist. See MemberContext.Member.MemberInfo for details.",
    onBehalfOf: MemberContext.MemberId
  }

  result MemberRegistered is {
    memberId: MemberContext.MemberId briefly "Unique identifier returned by RegisterMember command",
    info: MemberContext.EditableInfo,
    meta: MemberMetaInfo
  }

  group Member is {
    input RegisterMember acquires command UserInterface.RegisterMember
    output DisplayMemberDetails presents result MemberRegistered
  }

  handler MemberHandler is {
    on command RegisterMember from user DemoMember {
      tell command UserInterface.RegisterMember to context ImprovingApp.MemberContext
    }
    on event MemberRegistered from context ImprovingApp.MemberContext {
      tell result ImprovingApp.MemberContext.MemberRegistered to
        projector ImprovingApp.MemberContext.MemberViews
    }
  }

  command PurchaseProduct is {
    productSku: ProductContext.SKU
  }
  type ProductPurchased is {
    productSku: ProductContext.SKU,
    productInfo: ProductContext.ProductInfo,
    productMeta: ProductContext.ProductMetaInfo
  }
  result ProductsPurchased is {
    products: ProductPurchased*
  }

  group Product is {
    input PurchaseProduct acquires command UserInterface.PurchaseProduct
    output DisplayPurchases presents result ProductsPurchased
  }

  handler ProductHandler is {
    on command PurchaseProduct from user Member {
      tell command PurchaseProduct to context ProductContext
    }
    on event ProductContext.ProductPurchased
    from context ProductContext {
      tell result ProductContext.ProductCreated to projector ProductContext.ProductViews
    }
  }
}

application CommandLine is {
  command StartScenario is {
    json: String
  }
  result ScenarioStarted is {
    json: String
  }

  group Scenario is {
    input StartScenario acquires command CommandLine.StartScenario
    output DisplayScenario presents result CommandLine.ScenarioStarted
  }

  handler CurlHandler is {
    on command StartScenario from user Member {
      tell command CommandLine.StartScenario to context GatewayContext
    }
    on event ScenarioStarted from context ImprovingApp.GatewayContext {
      "display in terminal"
    }
  }
}
