application UserInterface is {
  command EstablishOrganization is {
    info: OrganizationContext.EditableInfo briefly "Details about an organization required for it to exist. See OrganizationContext.Organization.Info for details.",
    tenant: TenantId,
    parent: Parent?,
    admins: AdminList,
    members: MemberList,
    owners: OwnerList,
    contacts: ContactList,
    onBehalfOf: UserContext.UserId briefly "The Id of the User who is issuing the command."
  } briefly "If parentOrg is not defined you are creating a BaseOrganization. If it is, you are creating a SubOrganization."

  result OrganizationEstablished is {
    organizationId: OrganizationId briefly "Unique identifier returned by EstablishOrganization command",
    info: OrganizationContext.Info,
    parent: Parent?,
    admins: AdminList,
    members: MemberList,
    owners: OwnerList,
    contacts: ContactList,
    meta: OrganizationContext.MetaInfo
  }

  group Organization is {
    input EstablishOrganization is {
      acquires command UserInterface.EstablishOrganization
    }
    output DisplayOrganization is {
      presents result OrganizationEstablished
    }
  }

  handler OrgHandler is {
    on command EstablishOrganization from user OrgOwner {
      then tell command UserInterface.CreateOrganization to
        context ImprovingApp.OrganizationContext
    }
    on event OrganizationEstablished
    from context ImprovingApp.OrganizationContext {
      then {
        tell result ImprovingApp.OrganizationContext.OrganizationEstablished to
          projector ImprovingApp.OrganizationContext.OrganizationViews,
        "present Organization.DisplayOrganization"
      }
    }
  }

  command RegisterMember is {
    info: UserContext.EditableMemberInfo briefly "Details about a member required for it to exist. See UserContext.EditableMemberInfo for details.",
    onBehalfOf: UserContext.UserId
  }

  result MemberRegistered is {
    memberId: UserContext.UserId briefly "Unique identifier returned by RegisterMember command",
    info: UserContext.EditableMemberInfo,
    meta: UserContext.UserMetaInfo
  }

  command RegisterAdmin is {
    info: UserContext.EditableAdminInfo briefly "Details about an admin required for it to exist. See UserContext.EditableAdminInfo for details.",
    onBehalfOf: UserContext.UserId
  }

  result AdminRegistered is {
    UserId: UserContext.UserId briefly "Unique identifier returned by RegisterAdmin command",
    info: UserContext.EditableAdminInfo,
    meta: UserContext.UserMetaInfo
  }

  group Member is {
    input RegisterMember is {
      acquires command UserInterface.RegisterMember
    }
    output DisplayMemberDetails is {
      presents result UserInterface.MemberRegistered
    }
  }

  handler MemberHandler is {
    on command UserInterface.RegisterMember from user DemoMember {
      then tell command UserInterface.RegisterMember to
        context ImprovingApp.UserContext
    }
    on event MemberRegistered
    from context ImprovingApp.UserContext {
      then {
        tell result ImprovingApp.UserInterface.MemberRegistered to
          projector ImprovingApp.UserInterface.MemberViews,
        "present Member.DisplayMemberDetails"
      }
    }
  }

  command PurchaseProduct is {
    productSku: ProductContext.SKU
  }
  type ProductPurchased is {
    productSku: ProductContext.SKU,
    productInfo: ProductContext.ProductInfo,
    productMeta: ProductContext.ProductMetaInfo
  }
  result ProductsPurchased is {
    products: ProductPurchased*
  }

  group Product is {
    input PurchaseProduct is {
      acquires command UserInterface.PurchaseProduct
    }
    output DisplayPurchases is {
      presents result ProductsPurchased
    }
  }

  handler ProductHandler is {
    on command PurchaseProduct from user Member {
      then tell command PurchaseProduct to
        context ProductContext
    }
    on event ProductContext.ProductPurchased
    from context ProductContext {
      then {
        tell result ProductContext.ProductCreated to
          projector ProductContext.ProductViews,
        "present Product.DisplayPurchase"
      }
    }
  }
}

application CommandLine is {
  command StartScenario is {
    json: String
  }
  result ScenarioStarted is {
    json: String
  }

  group Scenario is {
    input StartScenario is {
      acquires command CommandLine.StartScenario
    }
    output DisplayScenario is {
      presents result CommandLine.ScenarioStarted
    }
  }

  handler CurlHandler is {
    on command StartScenario from user Member {
      then tell command CommandLine.StartScenario to
        context GatewayContext
    }
    on event ScenarioStarted
    from context ImprovingApp.GatewayContext {
      then "display in terminal"
    }
  }
}